plugins {
    id 'java'
    id 'idea'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'org.jetbrains.kotlin.jvm'
    id 'org.jetbrains.kotlin.plugin.serialization'
    id 'org.jetbrains.kotlin.kapt'
    id "com.github.ben-manes.versions" version "0.36.0"
    id "org.jetbrains.dokka" version "1.4.20"
}

apply from: 'https://raw.githubusercontent.com/MineInAbyss/shared-gradle/master/common.gradle'

allprojects {
    group = 'com.mineinabyss'
    version = plugin_version

    repositories {
        maven { url 'https://papermc.io/repo/repository/maven-public/' }
        maven { url 'https://repo.codemc.io/repository/nms/' }
    }

    compileKotlin {
        kotlinOptions {
            jvmTarget = "1.8"
            freeCompilerArgs += "-Xopt-in=kotlinx.serialization.ExperimentalSerializationApi"
        }
    }
    compileTestKotlin { kotlinOptions { jvmTarget = "1.8" } }
}

repositories {
    mavenCentral()
    jcenter()
    maven { url 'https://erethon.de/repo/' } //HeadLib
    maven { url "https://repo.dmulloy2.net/nexus/repository/public/" } //ProtocolLib
    maven { url 'https://maven.sk89q.com/repo/' } //WorldGuard/Edit
    maven { url 'https://repo.mineinabyss.com/releases'}
    maven { url 'https://jitpack.io' }
//    mavenLocal()
}

dependencies {
    compileOnly "com.destroystokyo.paper:paper-api:$server_version"
    compileOnly "org.spigotmc:spigot:$server_version" // NMS
    compileOnly "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    compileOnly platform("com.mineinabyss:kotlinspice:$kotlin_version+")
    compileOnly "com.github.okkero:skedule"
    compileOnly "org.nield:kotlin-statistics"
    implementation "com.mineinabyss:idofront-nms:0.5.8"
    compileOnly "com.mineinabyss:geary-spigot:0.2.26"
    compileOnly "com.mineinabyss:protocolburrito:0.1.12"

    compileOnly "de.erethon:headlib:3.0.2"
    compileOnly "com.sk89q.worldguard:worldguard-bukkit:7.0.2"
    compileOnly "com.comphenix.protocol:ProtocolLib:4.5.0"

    compileOnly project(":processor")
    kapt project(":processor")
}

shadowJar {
    archivesBaseName = "Mobzy"

    relocate 'com.derongan.minecraft.guiy', "${project.group}.${project.name}.guiy".toLowerCase()
    relocate 'com.mineinabyss.idofront', "${project.group}.${project.name}.idofront".toLowerCase()

    minimize {
        exclude(dependency('de.erethon:headlib:3.0.2'))
        exclude(dependency('com.github.WesJD.AnvilGUI:anvilgui:5e3ab1f721'))
    }
}
build.dependsOn shadowJar

publishing {
    repositories {
        maven {
            url = "https://repo.mineinabyss.com/releases"
            credentials {
                username = findProperty('mineinabyssMavenUsername')
                password = findProperty('mineinabyssMavenPassword')
            }
        }
    }
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
}
